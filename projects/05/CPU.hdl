
 CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output  
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
        /**
        * A Register
        */
        Mux16(a=instruction, b=outALU, sel=instruction[15], out=outMux1); // gate_01
        And(a=instruction[5], b=instruction[15], out=insWriteA); // gate_02
        Not(in=instruction[15], out=addWriteA); // gate_03
        Or(a=insWriteA, b=addWriteA, out=writeA); // gate_04
        ARegister(in=outMux1, load=writeA, out[0..14]=addressM, out=outRegA);
        /**
         * D Register
         */
        And(a=instruction[4], b=instruction[15], out=writeD); // gate_05
        DRegister(in=outALU, load=writeD, out=outRegD);
        /**
         * ALU
         */
        Mux16(a=outRegA, b=inM, sel=instruction[12], out=outMux2); // gate_06
        ALU(x=outRegD, y=outMux2, zx=instructio n[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outALU, out=outM, zr=zero, ng=negative);
        /**
         * PC
         */
        And(a=instruction[2], b=instruction[15], out=insJumpNeg); // gate_07
        And(a=instruction[1], b=instruction[15], out=insJumpZero); // gate_08
        And(a=instruction[0], b=instruction[15], out=insJumpPos); // gate_09
        Not(in=zero, out=notZero); // gate_10
        Not(in=negative, out=notNegative); // gate_11
        And(a=notZero, b=notNegative, out=positive); // gate_12
        And(a=insJumpPos, b=positive, out=jumpPos); // gate_13
        And(a=insJumpZero, b=zero, out=jumpZero); // gate_14
        And(a=insJumpNeg, b=negative, out=jumpNeg); // gate_15
        Or(a=jumpPos, b=jumpZero, out=jump1); // gate_16
        Or(a=jump1, b=jumpNeg, out=jump2); // gate_17
        Not(in=jump2, out=notJump2); // gate_18
        PC(in=outRegA, load=jump2, inc=notJump2, reset=reset, out[0..14]=pc);
        /**
         * WriteM
         */
         And(a=instruction[3], b=instruction[15], out=writeM);  // gate_19
 }